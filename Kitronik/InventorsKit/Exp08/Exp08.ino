/* Experiment 08: Wind power. Generate electricity by blowing on the fan 
 * connected to the motor.
 */

# define CLEAR_DISPLAY 10

/* maps pin states for the seven segments to digits */
const bool Numbers[11][7] = {
  { 1, 1, 1, 1, 1, 1, 0 }, // 0
  { 0, 1, 1, 0, 0, 0, 0 }, // 1
  { 1, 1, 0, 1, 1, 0, 1 }, // 2
  { 1, 1, 1, 1, 0, 0, 1 }, // 3
  { 0, 1, 1, 0, 0, 1, 1 }, // 4
  { 1, 0, 1, 1, 0, 1, 1 }, // 5
  { 1, 0, 1, 1, 1, 1, 1 }, // 6
  { 1, 1, 1, 0, 0, 0, 0 }, // 7
  { 1, 1, 1, 1, 1, 1, 1 }, // 8
  { 1, 1, 1, 0, 0, 1, 1 }, // 9
  { 0, 0, 0, 0, 0, 0, 0 }  // clear
};

/* setup pin state for given number */
int displayNumber(int numberToDisplay)
{
  int pin = 2;
  for (int segment = 0; segment < 7; ++segment)
  {
    digitalWrite(pin, Numbers[numberToDisplay][segment]);
    pin++;
  }
  return numberToDisplay;
}

void setup()
{
  for (int i = 2; i < 10; ++i)
    pinMode(i, OUTPUT);

  displayNumber(CLEAR_DISPLAY);
}

void loop()
{
  /* read voltage generated by wind turbine */
  int voltageValue = analogRead(A0);

  /* map analog input in (0, 1023) to (0, 9) */
  int displayValue = map(voltageValue, 0, 1023, 0, 9);
  /* display mapped voltage on seven segment display */
  displayNumber(displayValue);
  delay(100);
}